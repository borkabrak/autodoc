#!/usr/bin/env perl
#
#   Create a JSON file containing everything we can determine about the source (e.g., KAML element names)
#
#   The main thrust here is to assemble all the data we can into a file as JSON

use strict;
use 5.012;
use File::Basename;
use JSON;
use Data::Dumper;

my $apifile ="/cygdrive/c/TFS/Data\ Intelligence\ Tool/src/ditool/DEV/api.js";
my $specfile = "bin/spec.json";
my $analysis_file = "bin/analysis.json";
my $project_dir = "doc/";
my $json = JSON->new;
say "Creating spec..";

# Initialize spec from file
my $spec = {};
if (open SPEC, $specfile) {
    $spec = $json->decode(join('', <SPEC>));
} else {
    warn "Spec file $specfile not found:$!";
};

# Add to the spec anything else we can find out
##############################################################################

# KAML elements
say "Analyzing KAML elements..";
$spec->{ elements } = find_in_file(qr/(di_\w+)/i, $apifile);

# Non-element pages
my %elems = map {$_ => 1} @{$spec->{ elements }};
for (glob $project_dir . "/*.mkd"){
    my ($filename, undef, $suffix) = fileparse($_, ".mkd");
    next if $elems{ $filename };   
    push @{$spec->{ otherPages }}, $filename;
};

# write spec
open JSONFILE, ">", $analysis_file or die "Can't open $analysis_file:$!";
print JSONFILE $json->pretty->encode($spec);
say "Analysis created at $analysis_file";

#==============================================================================
sub find_in_file {
    # Returns a (reference to a) sorted, unique array of all matches for $regex
    # on the contents of $file
    my ($regex, $file) = @_;
    open (INPUT, "<", $file) or die "Can't open '$file':$!";
    my $input = join ' ' , <INPUT>;
    close INPUT;
    my @output = sort keys %{{ map { lc($_) => 1 } $input =~ /$regex/g }};
    return \@output;
}
